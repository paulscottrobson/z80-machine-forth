; *********************************************************************************************
; *********************************************************************************************
;
;		Name : 		extensions.asm
;		Date :		13th January 2019
;		Author : 	Paul Robson (paul@robsons.org.uk)
;		Purpose :	Core extensions
;		
; *********************************************************************************************
; *********************************************************************************************

; *********************************************************************************************
; 									b@a+ read byte A and increment
; *********************************************************************************************

@macro 	b@a+
		push 	de 									; save current TOS
		ld 		e,(hl) 								; read new TOS
		inc 	hl
		ld 		d,$00
@end

; *********************************************************************************************
; 											b@a byte read A
; *********************************************************************************************

@macro 	b@a
		push 	de 									; save current TOS
		ld 		e,(hl)								; read new TOS
		ld 		d,$00
@end

; *********************************************************************************************
;								b!a+ byte write TOS to A and increment
; *********************************************************************************************

@macro 	b!a+
		ld 		(hl),e 								; write out
		inc 	hl
		pop 	de 									; update TOS
@end

; *********************************************************************************************
;									b!a byte write TOS to A
; *********************************************************************************************

@macro 	b!a
		ld 		(hl),e 								; write out
		pop 	de 									; update TOS
@end

; *********************************************************************************************
;									b@ byte indirect read
; *********************************************************************************************

@macro 	b@
		ex 		de,hl 								; address to HL
		ld 		e,(hl)								; read it
		ld 		d,$00
@end

; *********************************************************************************************
;									b! byte indirect write
; *********************************************************************************************

@macro 	b! 
		ex 		de,hl 								; address to HL
		pop 	de 									; data to DE
		ld 		(hl),e 								; write it
		pop 	de 									; fix up TOS
@end

; *********************************************************************************************
;									nip drop second on stack
; *********************************************************************************************

@macro 	nip
		pop 	bc
@end
