; *********************************************************************************************
; *********************************************************************************************
;
;		Name : 		standard.asm
;		Date :		13th January 2019
;		Author : 	Paul Robson (paul@robsons.org.uk)
;		Purpose :	Normal macro words for the Machine Forth system
;		
; *********************************************************************************************
; *********************************************************************************************

; *********************************************************************************************
;									@ indirect read
; *********************************************************************************************

@macro 	@
		ex 		de,hl 								; address to HL
		ld 		e,(hl)								; read it
		inc 	hl
		ld 		d,(hl)
		dec 	hl
@end

; *********************************************************************************************
;									! indirect write
; *********************************************************************************************

@macro 	! 
		ex 		de,hl 								; address to HL
		pop 	de 									; data to DE
		ld 		(hl),e 								; write it
		inc 	hl
		ld 		(hl),d
		pop 	de 									; fix up TOS
@end

; *********************************************************************************************
; 										or logical OR
; *********************************************************************************************

@wordx 	or
		pop 	bc
		ld 		a,d
		or 		b
		ld 		d,a
		ld 		a,e
		or 		c
		ld 		e,a
@end

; *********************************************************************************************
; 										swap exchange top 2
; *********************************************************************************************

@macro 	swap
		ex 		de,hl 								; HL = tos
		ex 		(sp),hl 							; swap it
		ex 		de,hl 								; fix up
@end

